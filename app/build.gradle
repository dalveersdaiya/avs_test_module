apply plugin: 'com.android.application'

def versionMajor = 0
def versionMinor = 1
def versionPatch = 0
def versionDebug = new Date().format('ddMMyyyy')
def appPackageId = 'com.awrtechnologies.androidvibratorservice'
def abiCodes = ['armeabi-v7a': 4, 'arm64-v8a': 5, mips: 6, 'x86': 7, 'x86_64': 8]

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'
    defaultConfig {
        applicationId "${appPackageId}"
        minSdkVersion 15
        targetSdkVersion 27
        versionCode versionMajor * 1000 + versionMinor * 100 + versionPatch
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        multiDexEnabled true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    //  TODO: un-comment this before generate release for Google Play Store.
//    splits {
//        abi {
//            enable true
//            reset()
//            include 'armeabi-v7a', 'arm64-v8a', 'mips', 'x86', 'x86_64'
//            universalApk true
//        }
//    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    signingConfigs {
        debug {
        }
        release {
            storeFile file('/Users/dalveersinghdaiya/Documents/Repository/AndroidVibratorService/keystore/keystore.jks')
            storePassword "123456"
            keyAlias "datagetter"
            keyPassword "123456"

        }
    }

    lintOptions {
        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }

    configurations.all {
        resolutionStrategy.dependencySubstitution {
            substitute module('org.apache.commons:commons-io:1.3.2') with module('commons-io:commons-io:1.3.2')
        }
    }

    flavorDimensions "default"
    productFlavors {
        dev {
//            applicationIdSuffix ".dev"
            versionNameSuffix ".${versionDebug}.dev"
        }

        beta {
//            applicationIdSuffix ".beta"
            versionNameSuffix ".${versionDebug}.beta"
        }
        live {
//            applicationIdSuffix ".live"
            versionNameSuffix ".${versionDebug}.live"
        }
        free {

            versionNameSuffix ".${versionDebug}.free"

        }
    }
    android.applicationVariants.all { variant ->
        variant.resValue "string", "version_name", "v${variant.versionName}"
        variant.outputs.all { output ->
            def abiVersionName = output.getFilter(com.android.build.OutputFile.ABI);
            if (abiVersionName != null) {
                def baseAbiVersionCode = abiCodes.get(abiVersionName)
                if (baseAbiVersionCode != null) {
                    output.versionCodeOverride = baseAbiVersionCode * 10000 + variant.versionCode
                }
                outputFileName = "${abiVersionName}.${variant.versionName}.${variant.buildType.name}.apk"
            } else {
                outputFileName = "universal.${variant.versionName}.${variant.buildType.name}.apk"
            }
        }
    }
    dexOptions {
        javaMaxHeapSize "4g"
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    implementation project(":rotatetext")
    implementation 'com.android.support:appcompat-v7:27.1.0'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation 'com.android.support:design:27.1.0'
    implementation 'com.google.android.gms:play-services:12.0.1'
    implementation 'me.dm7.barcodescanner:zxing:1.9'
    implementation 'org.eclipse.paho:org.eclipse.paho.client.mqttv3:1.0.2'
    implementation 'org.bouncycastle:bcprov-jdk16:1.45'
    implementation 'org.apache.commons:commons-io:1.3.2'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.3.1'
    implementation 'commons-codec:commons-codec:1.10'
    implementation 'com.squareup.retrofit2:retrofit:2.0.2'
    implementation 'com.squareup.retrofit2:converter-gson:2.0.2'
    implementation 'com.google.firebase:firebase-messaging:12.0.1'
    implementation 'com.google.firebase:firebase-core:12.0.1'
    testImplementation 'junit:junit:4.12'
}
apply plugin: 'com.google.gms.google-services'